repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name "Cyclops Repo"
        url "https://dl.bintray.com/cyclopsmc/dev/"
    }
    maven {
        name = "bdew"
        url = "http://jenkins.bdew.net/maven"
    }
    maven {
        name = "Dankhax"
        url = "http://maven.epoxide.xyz"
    }
    maven {
        name = "Terrag"
        url = "http://maven.tterrag.com/"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Player Maven FS'
        url 'http://maven.ic2.player.to/'
    }
    maven {
        name = "Charset Maven"
        url = "http://charset.asie.pl/maven"
    }
}
dependencies {
    // Add something like 'cyclopscore_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local CyclopsCore version.
    if(project.hasProperty("cyclopscore_version_local")) {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${project.cyclopscore_version_local}:deobf"
    } else {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${config.cyclopscore_version}:deobf"
    }

    compile "net.bdew.thirdparty:rfapi:${config.rfapi_version}-mc1.8.9:api" // http://jenkins.bdew.net/maven/net/bdew/thirdparty/rfapi/
    deobfCompile "net.darkhax.tesla:Tesla:${config.minecraft_version}-${config.teslaapi_version}" // http://maven.epoxide.xyz/net/darkhax/tesla/Tesla/
    deobfCompile "com.enderio:EnderIO:${config.minecraft_version}-${config.enderio_version}" // http://maven.tterrag.com/com/enderio/EnderIO
    deobfCompile "com.enderio.core:EnderCore:${config.minecraft_version}-${config.endercore_version}" // http://maven.tterrag.com/com/enderio/EnderIO
    deobfCompile "slimeknights.mantle:Mantle:${config.minecraft_version}-${config.mantle_version}" // http://dvs1.progwml6.com/files/maven/slimeknights/mantle/Mantle/maven-metadata.xml
    deobfCompile "slimeknights:TConstruct:${config.minecraft_version}-${config.tconstruct_version}" // http://dvs1.progwml6.com/files/maven/slimeknights/TConstruct/maven-metadata.xml
    deobfCompile "net.sengir.forestry:forestry_${config.minecraft_version}:${config.forestry_version}" // http://maven.ic2.player.to/
    compile "net.industrial-craft:industrialcraft-2:${config.IC2_version}:dev" // http://maven.ic2.player.to/
    deobfCompile "pl.asie.charset:charset-api:${config.charset_version}" // http://charset.asie.pl/maven/pl/asie/charset/charset-api/maven-metadata.xml

    // Only needed for testing
    deobfCompile "pl.asie.charset:charset-lib:${config.charset_version}" // http://charset.asie.pl/maven/pl/asie/charset/charset-lib/maven-metadata.xml
    deobfCompile "pl.asie.charset:charset-wrench:${config.charset_version}" // http://charset.asie.pl/maven/pl/asie/charset/charset-wrench/maven-metadata.xml

    // Project lombok
    provided "org.projectlombok:lombok:1.14.4"
}


minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "run"
    
    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MC_VERSION@", project.config.minecraft_version
    replace "@FORGE_VERSION@", project.config.forge_version
    replace "@BUILD_NUMBER@", project.buildnumber
    replace "@CYCLOPSCORE_VERSION@", project.config.cyclopscore_version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "${config.mcp_mappings_version}"
}

if (project.buildnumber.equals("RELEASE"))
    version = "${config.minecraft_version}-${config.mod_version}"
else
    version = "${config.minecraft_version}-${config.mod_version}-${buildnumber}"

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, that is not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
